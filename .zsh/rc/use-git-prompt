# (You can assign these before or after this file)
HOST_COLOR=${SHOST_COLOR:-31}
USER_COLOR=${SUSER_COLOR:-24}
PROMPT_DIR_COLOR=${SPROMPT_DIR_COLOR:-64}

autoload -U vcs_info
zstyle ':vcs_info:*' enable git hg bzr svn
zstyle ':vcs_info:(hg*|git*):*' check-for-changes true
zstyle ':vcs_info:*' formats '%{%F{42}%}∇%{%F{26}%}%b%u%c'
zstyle ':vcs_info:*' branchformat '%b-%r'
zstyle ':vcs_info:git*:*' stagedstr '%{%F{19}%}●%{%}'
zstyle ':vcs_info:git*:*' unstagedstr '%{%F{196}%}✚%{%}'
zstyle ':vcs_info:git+post-backend:*' hooks tone-down-master
zstyle ':vcs_info:git+set-message:*' hooks check-untracked
# zstyle ':vcs_info:*+*:*' debug true

+vi-tone-down-master() { hook_com[branch]=${hook_com[branch]/#%master/ﬆ} }
git-ls-others() { git ls-files --others --exclude-standard 2>&- }
+vi-check-untracked() {
    git-ls-others | grep -q . || return
    local count=$(git-ls-others | wc -l | grep -v '^0$')
    hook_com[unstaged]="${hook_com[unstaged]}%{%F{82}%}…$count"
}

precmd() vcs_info
setopt promptsubst
export PROMPT='${vcs_info_msg_0_}%{%F{$HOST_COLOR}%}%B%#%b%{${reset_color}%} '
export RPROMPT=\
'%{%F{$USER_COLOR}%}%n%{${reset_color}%}@%{%F{$HOST_COLOR}%}%m%b:%{%F{$PROMPT_DIR_COLOR}%}%B%~'
# vim:ft=zsh
